
Процедура ЗаменитьУправляющиеСимволы(Строка) Экспорт
	
	Текст = СтрЗаменить(Текст, Символы.ВК + Символы.ПС, Символы.ПС);
	Текст = СтрЗаменить(Текст, Символ(7), Символы.ПС);
	Текст = СтрЗаменить(Текст, Символ(8), Символы.ПС);
	Текст = СтрЗаменить(Текст, Символы.ВТаб, Символы.ПС);
	Текст = СтрЗаменить(Текст, Символ(12), Символы.ПС);
	Текст = СтрЗаменить(Текст, Символы.ВК, Символы.ПС);
	Текст = СтрЗаменить(Текст, Символ(14), Символы.ПС);
	Текст = СтрЗаменить(Текст, Символ(28), " ");
	Текст = СтрЗаменить(Текст, Символ(29), " ");
	Текст = СтрЗаменить(Текст, Символ(30), " ");
	
КонецПроцедуры

// Добавляет к тексту в формате HTML другой текст в формате HTML
// Из добавляемого текста берется только внутренность тега <body>, если он там присутствует
// иначе добавляется весь ДобавляемыйТекстHTML.
Процедура ДобавитьТекстHTML(ТекстHTML, ДобавляемыйТекстHTML) Экспорт
	
	РазложенныйТекстHTML = РазложитьТекстHTML(ТекстHTML);
	РазложенныйДобавляемыйТекстHTML = РазложитьТекстHTML(ДобавляемыйТекстHTML);
	
	ТекстHTML = РазложенныйДобавляемыйТекстHTML.Заголовок
		+ РазложенныйТекстHTML.Тело
		+ РазложенныйДобавляемыйТекстHTML.Тело
		+ РазложенныйДобавляемыйТекстHTML.Окончание;
			
КонецПроцедуры

// Возвращает структуру "Заголовок, Тело, Окончание",
// где ТекстHTML = Заголовок + Тело + Окончание
// и Тело - содержимое тега body
//
Функция РазложитьТекстHTML(ТекстHTML) Экспорт
	
	НРегТекстHTML = НРег(ТекстHTML);
	
	ПозицияНачалаТела = 1;
	ПозицияОкончанияТела = СтрДлина(ТекстHTML);
	
	ПозицияНачалаТегаHTML = СтрНайти(НРегТекстHTML, "<html");
	Если ПозицияНачалаТегаHTML > 0 Тогда
		ПозицияОкончанияТегаHTML = СтроковыеФункцииКлиентСервер.НайтиПосле(НРегТекстHTML, ">", ПозицияНачалаТегаHTML);
		Если ПозицияОкончанияТегаHTML > 0 Тогда
			ПозицияНачалаТела = ПозицияОкончанияТегаHTML + 1;
		КонецЕсли;
	КонецЕсли;
	
	ПозицияНачалаТегаBODY = СтроковыеФункцииКлиентСервер.НайтиПосле(НРегТекстHTML, "<body", ПозицияНачалаТела - 1);
	Если ПозицияНачалаТегаBODY > 0 Тогда
		ПозицияОкончанияТегаBODY = СтроковыеФункцииКлиентСервер.НайтиПосле(НРегТекстHTML, ">", ПозицияНачалаТегаBODY);
		Если ПозицияОкончанияТегаBODY > 0 Тогда
			ПозицияНачалаТела = ПозицияОкончанияТегаBODY + 1;
		КонецЕсли;
	КонецЕсли;
	
	ПозицияНачалаЗакрывающегоТегаBODY = СтроковыеФункцииКлиентСервер.НайтиПосле(НРегТекстHTML, "</body>", ПозицияНачалаТела - 1);
	Если ПозицияНачалаЗакрывающегоТегаBODY > 0 Тогда
		ПозицияОкончанияТела = ПозицияНачалаЗакрывающегоТегаBODY - 1;
	Иначе
		ПозицияНачалаЗакрывающегоТегаHTML = СтроковыеФункцииКлиентСервер.НайтиПосле(НРегТекстHTML, "</html>", ПозицияНачалаТела - 1);
		Если ПозицияНачалаЗакрывающегоТегаHTML > 0 Тогда
			ПозицияОкончанияТела = ПозицияНачалаЗакрывающегоТегаHTML - 1;
		КонецЕсли;
	КонецЕсли;
	
	Заголовок = Лев(ТекстHTML, ПозицияНачалаТела - 1);
	Тело = Сред(ТекстHTML, ПозицияНачалаТела, ПозицияОкончанияТела - ПозицияНачалаТела + 1);
	Окончание = Сред(ТекстHTML, ПозицияОкончанияТела + 1);
	
	Результат = Новый Структура("Заголовок, Тело, Окончание", Заголовок, Тело, Окончание);
	
	Возврат Результат;
	
КонецФункции
